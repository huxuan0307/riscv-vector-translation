# 配置设置指令(vsetvli/vsetivl/vsetvl)



处理大量元素的一种常见方法是“剥离”，其中每次迭代处理一定数量的元素，迭代到所有元素被处理完为止。 RISC-V 向量规范为这种方法提供了直接、可移植的支持。应用程序指定要处理的元素总数（Application Vector Length, AVL）作为 vl 的候选值，硬件通过通用寄存器响应硬件每轮迭代处理的元素数量（通常较小）存储在 vl 中，每轮迭代处理的元素数量基于微架构实现和 vtype 寄存器的值设定。一个简单的循环结构，如**剥离和更改 SEW 的示例**所示，描述了软件处理剩余元素数量与硬件处理的每次迭代量的简单过程。 

向量拓展提供了一组指令来快速配置 vl 和 vtype 寄存器中的值以满足应用程序需求。 `vset{i}vl{i}`指令根据指令参数设置 vtype 和 vl 寄存器，并将 vl 的新值写入 rd。 

```assembly
vsetvli rd, rs1, vtypei # rd = new vl, rs1 = AVL, vtypei = new vtype setting 
vsetivli rd, uimm, vtypei # rd = new vl, uimm = AVL, vtypei = new vtype setting 
vsetvl rd, rs1, rs2 # rd = new vl, rs1 = AVL, rs2 = new vtype value
```

在OP-V主操作码下的向量配置指令的格式

![image-20210911145420752](C:\Users\胡轩\AppData\Roaming\Typora\typora-user-images\image-20210911145420752.png)

## 6.1 vtype寄存器编码

![image-20210903225547266](C:\Users\胡轩\AppData\Roaming\Typora\typora-user-images\image-20210903225547266.png)

> 表中展示的是RV32系统里的vtype寄存器布局，而通常来说，vill字段应该在XLEN-1bit位置。

| 位域     | 字段名称   | 字段描述             |
| -------- | ---------- | -------------------- |
| XLEN-1   | vill       | 为1，vtype则是无效值 |
| XLEM-2:8 |            | 保留                 |
| 7        | vma        | 向量掩码不可知       |
| 6        | vta        | 向量末尾不可知       |
| 5:3      | vsew[2:0]  | 选定元素的位宽设置   |
| 2:0      | vlmul[2:0] | 向量寄存器组乘数设置 |

[^译注]: **0.7.1**里的vediv字段被移除

新的 vtype 设置编码在 vsetvli 和 vsetivli 指令的立即数字段中，编码在 vsetvl 指令的 rs2 寄存器中。 

```assembly
# vset{i}vl{i}指令中vtypei字段推荐使用的汇编名称
e8		# SEW=8b
e16 	# SEW=16b 
e32 	# SEW=32b 
e64 	# SEW=64b 
e128 	# SEW=128b
e256 	# SEW=256b 
e512 	# SEW=512b 
e1024 	# SEW=1024b

mf8 # LMUL=1/8 
mf4 # LMUL=1/4 
mf2 # LMUL=1/2 
m1 	# LMUL=1, m未指定时的默认值
m2 	# LMUL=2 
m4 	# LMUL=4 
m8 	# LMUL=8

Examples:
	vsetvli t0, a0, e8			# SEW= 8, LMUL=1 
	vsetvli t0, a0, e8, m2 		# SEW= 8, LMUL=2
	vsetvli t0, a0, e32, mf2	# SEW=32, LMUL=1/2
```

vsetvl 变体的指令与 vsetvli 类似，不同之处在于它从 rs2 获取 vtype 值并可用于恢复上下文。

如果CPU实现不支持 vtype 设置，则在 vtype 中设置 vill 位，将 vtype 中的其余位设置为零，并且 vl 寄存器也设置为零。

> 早期的草案在将 vtype 设置为非法值时需要一个trap。 但是，这会在 CSR 写入 ISA 时添加第一个数据依赖trap。 CPU实现可以选择捕获将非法值写入 vtype 而未设置 vill 的操作，以允许仿真支持新配置以实现前向兼容性。当前方案通过检查给定vtype设置中的vill字段是否为0，来检查受支持的向量单元配置，这是开销较小的轻量级检查。



## 6.2 AVL 编码

新向量长度的设定基于AVL，AVL编码在`vsetvli`和`vsetvl`指令的rs1和rd字段

表.8 vsetvli和vsetvl指令中使用的AVL字段

|  rd  | rs1  |   AVL的值    |           对vl的影响            |
| :--: | :--: | :----------: | :-----------------------------: |
|  -   | !x0  | x[rs1]中的值 |         正常的剥离处理          |
| !x0  |  x0  |      ~0      |         将vl设置为VLMAX         |
|  x0  |  x0  | vl寄存器的值 | 保持已有的vl值，vtype可能会改变 |

当rs1不是x0时，AVL是一个无符号整数，保存在rs1指定的x寄存器中，新的vl值也写入rd指定的x寄存器。

当 rs1=x0 但 rd!=x0 时，最大的无符号整数值 (~0) 用作 AVL，将产生的 VLMAX 写入 vl 和 rd 指定的 x 寄存器。 

当 rs1=x0 且 rd=x0 时，指令的操作就像将 vl 中的当前向量长度用作 AVL，并将结果值写入 vl，但不写入目标寄存器。 这种形式只能在 VLMAX 时使用，因此 vl 实际上没有被新的 SEW/LMUL 比率改变。 使用新 SEW/LMUL 比值的指令会导致 VLMAX 更改，这样的指令是**保留**的。 在这种情况下，CPU实现可能会设置 vill。 

> vsetvl{i}指令的最后一种形式允许在保持当前 vl 的同时更改 vtype 寄存器，前提是 VLMAX 不减少。 选择此设计是为了确保 vl 始终保持当前 vtype 设置的合法值。 当前的 vl 值可以从 vl CSR 中读取。 如果新的 SEW/LMUL 比值导致 VLMAX 缩小，会通过此指令减小 vl 值，因此这种情况已被**保留**，因为不清楚这是否为一个普遍有用的操作。否则CPU实现可以假设 vl 不会被这个指令改变来优化他们的微架构。 

对于 vsetivli 指令，AVL 在 rs1 字段中编码为 5 位零扩展立即数 (0—31)。

> vsetivli 指令的 AVL 字段在常规 CSR指令的立即数字段。 

> 当向量的维度较小时，vsetivli 指令能够提供更紧凑的代码，并且已知适合向量寄存器，因此不需要剥离开销。



##  6.3 设置vl寄存器的约束

vset{i}vl{i} 指令优先根据 vtype 参数设置 VLMAX，然后设置 vl 遵守以下约束：

+ vl = AVL, if AVL ≤ VLMAX

+ ceil(AVL / 2) ≤ vl ≤ VLMAX, if AVL < (2 * VLMAX)

+ vl = VLMAX, if AVL ≥ (2 * VLMAX)

+ 任何给定实现对相同输入 AVL 和 VLMAX 值的约束结果是确定的

+ 这些特定属性遵循先前的规则： 
  + vl = 0 if AVL = 0
  + vl > 0 if AVL > 0
  + vl ≤ VLMAX
  + vl ≤ AVL
  + 用作 vset{i}vl{i} 的 AVL 参数从 vl 读取，会保持vl中的值，前提是结果的VLMAX 等于读取 vl 时的VLMAX 值

> vl 设置规则设计得足够严格，为了在 AVL ≤ VLMAX 的寄存器溢出和上下文交换中保留 vl 行为，但又足够灵活，可以提高 AVL > VLMAX 的向量通道利用率。

> 例如，第二条约束中允许当AVL满足 VLMAX < AVL < 2\*VLMAX 设置 vl = ceil(AVL / 2)，以便在剥离循环的最后两次迭代中均匀分配工作。这条约束确保减少循环的第一次剥离迭代使用所有迭代中最大的向量长度，即使在 AVL < 2\*VLMAX 的情况下也是如此。 这样的约束让软件不再需要显式计算在剥离循环期间观察到的vl的运行最大值。 当VLMAX < AVL < 2\*VLMAX时，第二条约束还允许实现将 vl 设置为 VLMAX。



## 6.4 向量剥离和改变SEW值举例

SEW 和 LMUL 设置可以动态更改，以在单个循环中为混合宽度操作提供高吞吐量。 

```assembly
# 举例：加载16bits数值，做widen multiply后拓展到32bits，再将结果右移3位，存储32bits数值
# 输入：
# 	a0 保存要处理的元素总数
# 	a1 保存源数组的地址
#   a2 保存目的数组的地址

loop: 
	vsetvli a3, a0, e16, m4, ta, ma # vtype = 16-bit integer vectors; 
					# also update a3 with vl (# of elements this iteration)
    vle16.v v4, (a1) 	# Get 16b vector
    slli t1, a3, 1 		# Multiply # elements this iteration by 2 bytes/source element  
    add a1, a1, t1		# Bump pointer
    vwmul.vx v8, v4, x10 # Widening multiply into 32b in <v8--v15>

    vsetvli x0, x0, e32, m8, ta, ma # Operate on 32b values 
    vsrl.vi v8, v8, 3 
    vse32.v v8, (a2)	# Store vector of 32b elements
    slli t1, a3, 2 		# Multiply elements this iteration by 4 bytes/destination element
    add a2, a2, t1 		# Bump pointer
    sub a0, a0, a3 		# Decrement count by vl
    bnez a0, loop		# Any more?
 
```



